#CLASS {warrior} OPEN

#ACTION {Aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa warrior.tt$}
{
	#nop /* this action can be removed but is used to document the file and   */;
	#nop /* it's functionality.                                               */;
	#nop /*                                                                   */;
	#nop /* external dependencies...                                          */;
	#nop /*      combat.tt                                                    */;
	#nop /*           var {t[]} used for targeting certain skills             */;
	#nop /*           don't spam switch opponents with asst                   */;
	#nop /*                                                                   */;
	#nop /*                                                                   */;
	#nop /* aliases and usage                                                 */;
	#nop /*      {shi}                                                        */;
	#nop /*           shield punches engaged mob                              */;
	#nop /*                                                                   */;
	#nop /*      {autobash <on/off/blank>}                                    */;
	#nop /*           turns autobash triggers on or off and reports the       */;
	#nop /*           status on use.                                          */;
	#nop /*                                                                   */;
	#nop /*      {autosp <on/off/blank>}                                      */;
	#nop /*                                                                   */;
	#nop /*      {resc <digit>}                                               */;
	#nop /*           rescues the target number from the targeting scripts    */;
	#nop /*           in combat.tt                                            */;
	#nop /*                                                                   */;
	#nop /*                                                                   */;
	#nop /* variables set by class                                            */;
	#nop /*      {autobash}  {0}                                                  */;
	#nop /*           is autobash on                                          */;
	#nop /*                                                                   */;
	#nop /*      {warior} {1}                                                 */;
	#nop /*           sets a var to identify that the warior script is loaded */;
	#nop /*                                                                   */;
	#nop /*      {sp}  {0}                                                    */;
	#nop /*           is shield punch and other extra attacks on for bashing  */;
	#nop /*                                                                   */
}
{1}

#ACTION {%* avoids your bash, you topple over and fall to the ground.$}
{
	st
}
{5}

#ACTION {%* clambers to %w feet}
{
	#if {&autobash}
	{
		#if {$autobash}
		{
			bash
		}
	}
}
{5}

#ACTION {%i{(?:$$t[1] sends .* sprawling with a powerful bash.)|(is sent sprawling)|(?:avoids being bashed by $$t[1], who loses his balance and falls.)|(?:$$t[1] attacks)}}
{
	asst
}
{5}

#ACTION {PANIC!  You couldn't escape!}
{
	flee
}
{5}

#ACTION {You try to bash %*, only to sprawl completely}
{
	st
}
{5}

#ACTION {You {clamber|rise} to your feet}
{
	#if {&autobash}
	{
		#if {$autobash}
		{
			bash
		}
	}
}
{5}

#ACTION {Your bash at %*}
{
	#if {&sp}
	{
		#switch {$sp}
		{
			#case {1}
			{
				shi;
				#echo shield punch;
				#math {sp} {$sp + 1}
			};
			#case {2}
			{
				kick;
				#echo kick;
				#math {sp} {$sp + 1}
			};
			#case {3}
			{
				headbu;
				#echo headbutt;
				#math {sp} {$sp + 1}
			}
		};
		#if {$sp > 3}
		{
			#variable {sp} 1
		}
	}
}
{5}

#ACTION {^You pass clean through}
{
	#if {&autobash}
	{
		#if {$autobash}
		{
			bash
		}
	}
}
{5}

#alias {%i{bashset (n|p)(?:o(?:r(?:m(?:al?)?)?)?|o(?:w(?:e(?:rs?)?)?)?)?}}
{
	#switch {"%2"}
	{
		#case {"n"}
		{
			#var {bash} {bas};
		};
		#case {"p"}
		{
			#var {bash} {bh};
		}
	}
}

#alias {bash}
{
	$bash %1
}

#ALIAS {shi}
{
	shieldpunch
}
{5}

#ALIAS {{autobash(?: (\w+))?}}
{
	#switch {"%2"}
	{
		#case {"on"}
		{
			#variable {autobash} {1};
		};
		#case {"off"}
		{
			#variable {autobash} {0};
		}
	};
	#if {$autobash}
	{
		#variable saystat on
	};
	#else
	{
		#variable saystat off
	};
	#showme You say auto bashing is $saystat;
	#unvariable saystat
}
{5}

#ACTION {%i{(?:.* (?:wounds|misses) (?:$rlistcomp))|(?:sends (?:$rlistcomp) sprawling with a powerful bash.)|((?:$rlistcomp) is sent sprawling)|(?:(?:$rlistcomp) avoids being bashed by .*?, who loses his balance and falls.)}}
{
};

#ali {%i{resl? (a|r) ([a-z]+)}}
{
	#var {rchklist} {0};
	#list {rlist} {find} {%3} {rchklist};
	#if {"%2" == "a" && "$rchklist" <= "0"}
	{
		#list {rlist} {add} {%3};
	};
	#if {"%2" == "r" && $rchklist <= 0}
	{
		#list {rlist} {delete} {$rchklist};
	};
	#list {rlist} {length} {rlistlen};
	#var {rlistcomp} {};
	#if {$rlistlen > 0}
	{
		#var {rlistcomp} {$rlist[1]};
		#if {$rlistlen > 1}
		{
			#loop {2} {$rlistlen} {i} {#var {rlistcomp} {$rlistcomp|$rlist[$i]}};
		};
	};
};

#ALIAS {{autosp(?: (\w+))?}}
{
	#switch {"%2"}
	{
		#case {"on"}
		{
			#variable {sp} {1}
		};
		#case {"off"}
		{
			#variable {sp} {0}
		}
	};
	#if {$sp}
	{
		#variable saystat on
	};
	#else
	{
		#variable saystat off
	};
	#showme You say shieldpunch/kick/headbutt is $saystat when bashing;
	#unvariable saystat
}
{5}

#ALIAS {{resc \d}}
{
	rescue $t[%2]
}
{5}

#GAG              {You miss %*}
#var {bash} {bas};
#VARIABLE         {autobash}  {0}
#VARIABLE         {sp}  {0}
#VARIABLE         {warassist} {1}
#VARIABLE         {warior}  {1}
#var {rlist} {}
#var {rlistcomp} {}

#CLASS {warrior} CLOSE
