#CLASS {warrior} OPEN

#ACTION {Aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa warrior.tt$}
{
	#nop /* this action can be removed but is used to document the file and   */;
	#nop /* it's functionality.                                               */;
	#nop /*                                                                   */;
	#nop /* external dependencies...                                          */;
	#nop /*      combat.tt                                                    */;
	#nop /*           var {t[]} used for targeting certain skills             */;
	#nop /*           don't spam switch opponents with asst                   */;
	#nop /*                                                                   */;
	#nop /*                                                                   */;
	#nop /* aliases and usage                                                 */;
	#nop /*      {shi}                                                        */;
	#nop /*           shield punches engaged mob                              */;
	#nop /*                                                                   */;
	#nop /*      {autobash <on/off/blank>}                                    */;
	#nop /*           turns autobash triggers on or off and reports the       */;
	#nop /*           status on use.                                          */;
	#nop /*                                                                   */;
	#nop /*      {autosp <on/off/blank>}                                      */;
	#nop /*                                                                   */;
	#nop /*      {resc <digit>}                                               */;
	#nop /*           rescues the target number from the targeting scripts    */;
	#nop /*           in combat.tt                                            */;
	#nop /*                                                                   */;
	#nop /*                                                                   */;
	#nop /* variables set by class                                            */;
	#nop /*      {bash}  {0}                                                  */;
	#nop /*           is autobash on                                          */;
	#nop /*                                                                   */;
	#nop /*      {warior} {1}                                                 */;
	#nop /*           sets a var to identify that the warior script is loaded */;
	#nop /*                                                                   */;
	#nop /*      {sp}  {0}                                                    */;
	#nop /*           is shield punch and other extra attacks on for bashing  */;
	#nop /*                                                                   */
}
{1}

#ACTION {%* avoids your bash, you topple over and fall to the ground.$}
{
	st
}
{5}

#ACTION {%* clambers to %w feet}
{
	#if {&bash}
	{
		#if {$bash}
		{
			bash
		}
	}
}
{5}

#ACTION {PANIC!  You couldn't escape!}
{
	flee
}
{5}

#ACTION {You try to bash %*, only to sprawl completely}
{
	st
}
{5}

#ACTION {You {clamber|rise} to your feet}
{
	#if {&bash}
	{
		#if {$bash}
		{
			bash
		}
	}
}
{5}

#ACTION {Your bash at %*}
{
	#if {&sp}
	{
		#switch {$sp}
		{
			#case {1}
			{
				shi;
				#echo shield punch;
				#math {sp} {$sp + 1}
			};
			#case {2}
			{
				kick;
				#echo kick;
				#math {sp} {$sp + 1}
			};
			#case {3}
			{
				headbu;
				#echo headbutt;
				#math {sp} {$sp + 1}
			}
		};
		#if {$sp > 3}
		{
			#variable {sp} 1
		};
	}
}
{5}

#ACTION {^You pass clean through}
{
	#if {&bash}
	{
		#if {$bash}
		{
			bash
		}
	}
}
{5}

#ALIAS {shi}
{
	shieldpunch
}
{5}

#ALIAS {{autobash(?: (\w+))?}}
{
	#switch {"%2"}
	{
		#case {"on"}
		{
			#variable bash 1
		};
		#case {"off"}
		{
			#variable bash 0
		}
	};
	#if {$bash}
	{
		#variable saystat on
	};
	#else
	{
		#variable saystat off
	};
	#showme You say auto bashing is $saystat;
	#unvariable saystat
}
{5}

#ALIAS {{autosp(?: (\w+))?}}
{
	#switch {"%2"}
	{
		#case {"on"}
		{
			#variable {sp} {1}
		};
		#case {"off"}
		{
			#variable {sp} {0}
		}
	};
	#if {$sp}
	{
		#variable saystat on
	};
	#else
	{
		#variable saystat off
	};
	#showme You say shieldpunch/kick/headbutt is $saystat when bashing;
	#unvariable saystat
}
{5}

#ALIAS {{resc \d}}
{
	rescue $t[%2]
}
{5}

#GAG              {You miss %*}
#VARIABLE         {bash}  {0}
#VARIABLE         {sp}  {0}
#VARIABLE         {warior}  {1}

#CLASS {warrior} CLOSE
