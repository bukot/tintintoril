#CLASS {combat} OPEN

#ACTION {Aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa general.tt$}
{
	#nop /* this action can be removed but is used to document the file and   */;
	#nop /* it's functionality.                                               */;
	#nop /* external dependencies...                                          */;
	#nop /*      general.tt                                                   */;
	#nop /*           var {enemy} used to determine if we are in combat so we */;
	#nop /*           don't spam switch opponents with asst                   */;
	#nop /*                                                                   */;
	#nop /*                                                                   */;
	#nop /* aliases and usage                                                 */;
	#nop /*      {t<number> <name|none>}                                      */;
	#nop /*           this is the targeting system values are stored in an    */;
	#nop /*           array type variable named t.                            */;
	#nop /*           the name is optional if omitted will report the current */;
	#nop /*           value of t<number> if none is supplied it will clear it */;
	#nop /*           otherwise it sets t<number> to the give name            */;
	#nop /*                                                                   */;
	#nop /*      {tclear}                                                     */;
	#nop /*           clears the target list                                  */;
	#nop /*                                                                   */;
	#nop /*      {tl}                                                         */;
	#nop /*           displays all values stored in the target list           */;
	#nop /*                                                                   */;
	#nop /*      {asst <name|num>}                                            */;
	#nop /*           assist that can accept a name or target number.  if the */;
	#nop /*           <name|num> is omitted it defaults to target 1 <1>       */;
	#nop /*                                                                   */;
	#nop /*                                                                   */;
	#nop /* variables set by class                                            */;
	#nop /*      {combat}                                                     */;
	#nop /*           variable used to test for combat class being loaded     */;
	#nop /*                                                                   */;
	#nop /*      {doasst}                                                     */;
	#nop /*           used to track if asst should fire the assist or not     */;
	#nop /*                                                                   */;
	#nop /*      {t}                                                          */;
	#nop /*           stores the target information in array format           */;
	#nop /*                                                                   */;
}
{5}

#ACTION {You stagger, and fall to your knees!}
{
	st
}
{5}

#ACTION {^Assist whom?$}
{
	#variable {doasst} {1}
}
{5}

#ACTION {^That person is not fighting anyone.$}
{
	#variable {doasst} {1}
}
{5}

#ACTION {^You manage to, with complete incompetence, throw yourself head first directly into the ground!}
{
	#nop /* trip fail trigger                                           */;
	st
}
{5}

#ACTION {{.* sends you sprawling.$}}
{
	st
}
{5}

#ACTION {{.* tripping you.$}}
{
	st
}
{5}

#ALIAS {%i{^t([0-9]+)(?: (\w*))?$}}
{
	#variable {ttest1} {%2};
	#variable {ttest2} {%3};
	#variable {ttest3} {$t.%2};
	#if {"$ttest2" != ""}
	{
		#if {"$ttest3" != ""}
		{
			#line {substitute} {variable} {#unsubstitute {~%i%%1$ttest3%%2}}
		};
		#if {"$ttest2" != "none"}
		{
			#if {$ttest1 == 1}
			{
				#line {substitute} {variable} {#substitute {~%i%%1$ttest2%%2} {%%1<134>(t1)$ttest2<088>%%2}}
			};
			#else
			{
				#line {substitute} {variable} {#substitute {~%i%%1$ttest2%%2} {%%1<134>(t$ttest1)<088>$ttest2%%2}}
			}
		};
		#else
		{
			#showme <168>Target $ttest1 ($ttest3) is now: $ttest2
		};
		#if {"$ttest2" == "none"}
		{
			#unvariable t[$ttest1]
		};
		#else
		{
			#if {"$ttest2" != ""}
			{
				#variable {t[$ttest1]} {$ttest2};
			}
		}
	};
	#showme {you say target$ttest1 is $t.$ttest1};
	#unvariable ttest1;
	#unvariable ttest2;
	#unvariable ttest3
}
{5}

#ALIAS {%i{^tclear$}}
{
	#foreach {$t} {tcli}
	{
		#regexp {$tcli} {{[0-9]+$}} {t$tcli none}
	};
	#unvariable {tcli}
}
{5}

#ALIAS {%i{^tl$}}
{
	#foreach {$t} {tli}
	{
		#regexp {$tli} {{[0-9]+$}} {t$tli}
	};
	#unvariable {tli}
}
{5}

#ALIAS {{asst(?: (\d*))?}}
{
	#if {"$enemy" != ""}
	{
		#variable {doasst} {0}
	};
	#else
	{
		#variable {doasst} {1}
	};
	#if {$doasst}
	{
		#if {%2 && "$t[%2]" != ""}
		{
			ass $t[%2];
			#variable {doasst} {0}
		};
		#else
		{
			ass $t.1;
			#variable {doasst} {0}
		}
	}
}
{5}

#VARIABLE         {combat}  {1}
#VARIABLE         {doasst}  {1}
#VARIABLE         {t}  {}

#CLASS {combat} CLOSE
