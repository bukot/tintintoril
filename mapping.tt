#CLASS {mapping} OPEN

#ACTION {Suddenly the darkness falls revealing different surroundings.}
{
	#nop /*	this is the trigger for shifting to activate mapport /*;
	#map leave;
	load mapport
}
{5}

#ACTION {You try to tell yourself something}
{
	#variable mapsave 1
}
{5}

#ACTION {^{You find traces of tracks leading (\w)\w*\.}}
{
	#map move %2
}
{5}

#ACTION {{You (?:escape|flee) (\w*)!}}
{
	#switch {"%2"}
	{
		#case {"westward"}
		{
			#map move w;
			#variable backflee e
		};
		#case {"eastward"}
		{
			#map move e;
			#variable backflee w
		};
		#case {"southward"}
		{
			#map move s;
			#variable backflee n
		};
		#case {"northward"}
		{
			#map move n;
			#variable backflee s
		};
		#case {"upward"}
		{
			#map move u;
			#variable backflee d
		};
		#case {"downward"}
		{
			#map move d;
			#variable backflee u
		}
	};
	#if {&slamit[1]}
	{
		#if {$slamit[1]}
		{
			$backflee;
			bodyslam $slamit[2];
			#variable slamit 0
		}
	};
	#else
	{
		#if {&pracflee}
		{
			#if {"$pracflee" == "flame"}
			{
				spellup;
				a
			}
		}
	}
}
{5}

#ACTION {{You follow \w* (\w)\w*}}
{
	#map move %2
}
{5}

#ALIAS {disembark}
{
	load mapport;
	disemb
}
{5}

#ALIAS {jump {[N|S|E|W|D|U|n|s|e|w|u|d]}}
{
	jum %1;
	#map move %1
}
{5}

#ALIAS {mapupdate}
{
	#map map 110x24 mapdisp.txt a
}
{5}

#ALIAS {mcolor %w}
{
	#switch {"%1"}
	{
		#case {"rd"}
		{
			#variable {rmcolr} {230}
		};
		#case {"shop"}
		{
			#variable {rmcolr} {160}
		};
		#default
		{
			#variable {rmcolr} {170}
		}
	};
	#map set roomcolor <$rmcolr>;
	#unvariable rmcolr
}
{5}

#ALIAS {mdoor {[N|S|E|W|U|D|n|s|e|w|u|d]} %w}
{
	#map dig %1;
	#map exit %1 command {open %2 %1;%1}
}
{5}

#ALIAS {mfind %w %2}
{
	#switch {"%1"}
	{
		#case {"no"}
		{
			#map list {roomnote}{%*%2%*}
		};
		#case {"n"}
		{
			#map list {%*%2%*}
		}
	}
}
{5}

#ALIAS {mgoto}
{
	#map goto %1
}
{5}

#ALIAS {mhide {[N|S|E|W|U|D|n|s|e|w|u|d]}}
{
	#map exitflag %1 hide
}
{5}

#ALIAS {mmakeport}
{
	#map dig {en %1} %2
}
{5}

#ALIAS {mrun %1}
{
	#variable mapsave 0;
	#map run %1;
	t $me hello;
	#map map 110x24 mapdisp.txt a;
	#map write $mapname
}
{5}

#ALIAS {msetname %1}
{
	#map set roomname {%1}
}
{5}

#ALIAS {msetnote %1}
{
	#map set roomnote {%1}
}
{5}

#ALIAS {mstr}
{
	#map insert %1 void
}
{5}

#ALIAS {mundo}
{
	#map undo;
	mapupdate
}
{5}

#ALIAS {updatemap}
{
	#map write $mapname
}
{5}

#ALIAS {{en (hole|rift)}}
{
	load mapport;
	#map leave;
	ent %2
}
{5}

#EVENT {MAP ENTER ROOM}
{
	mapupdate;
	#if {$mapsave}
	{
		#if {$mapcount < 30}
		{
			#math mapcount {$mapcount + 1}
		};
		#else
		{
			#unvariable mapcount;
			updatemap
		}
	}
}

#EVENT {SESSION DISCONNECTED}
{
	updatemap
}

#VARIABLE         {mapsave}  {1}

#CLASS {mapping} CLOSE
