#CLASS {rogue} OPEN

#ACTION {Aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa rogue.tt$}
{
	#nop /* this action can be removed but is used to document the file and   */;
	#nop /* it's functionality.                                               */;
	#nop /* external dependencies...                                          */;
	#nop /*      none                                                         */;
	#nop /*                                                                   */;
	#nop /* aliases and usage...                                              */;
	#nop /*      {app <weapon> <poison>}                                      */;
	#nop /*           applies the specified poison to the specified weapon    */;
	#nop /*           if no poison is specified and this was used before      */;
	#nop /*           the last poison applied will be used.                   */;
	#nop /*                                                                   */;
	#nop /*      {autogar <on|off>}                                           */;
	#nop /*           will re-garrote a target if the target flips you off    */;
	#nop /*           if the on|off is omitted then it will report the status */;
	#nop /*           of auto garroting.                                      */;
	#nop /*                                                                   */;
	#nop /*      {autotrip <on|off>}                                          */;
	#nop /*           will re-trip a target they stand or if you miss         */;
	#nop /*           if the on|off is omitted then it will report the status */;
	#nop /*           of auto triping.                                        */;
	#nop /*                                                                   */;
	#nop /*      {bs <target>}                                                */;
	#nop /*           backstab the target                                     */
}
{1}

#ACTION {Your disguise falls apart!}
{
	#showme you say disguise is out!!
}
{5}

#ACTION {{(?:.+ clambers? to \w+ feet)|(?:.+ rises to \w+ feet.)}}
{
	#if {&roguetrip}
	{
		#if {$roguetrip}
		{
			trip
		}
	}
}
{5}

#ACTION {{(?:You manage to, with complete incompetence, throw yourself head first directly into the ground!)|(?:Unfortunately, a /w+ proves to be quite agile (?:his|her|it) self, and you go down as well.)}}
{
	st
}
{5}

#ACTION {{.* does a nifty move, flipping you onto your back!}}
{
	st;
	#if {$autogar}
	{
		gar $enemy
	}
}
{5}

#ACTION {{And just for the attempt, you fall down.}}
{
	st
}
{5}

#ACTION {{As your poison takes effect, (.*?) appears to go blind!}}
{
	#showme {you say %2 is poison blind!}
}
{5}

#ACTION {{You try to trip .+, only to sprawl completely THROUGH .+.}}
{
	st
}
{5}

#ALIAS {%i{^cl (1$|2$|(?:[0-9]{0,2}\.)?[a-z]+$)}}
{
	#switch {"%2"}
	{
		#case {"1"}
		{
			cl primaryweapon
		};
		#case {"2"}
		{
			cl secondaryweapon
		};
		#default
		{
			clean %2
		}
	}
}
{5}

#ALIAS {%i{app (1|2|(?:[0-9]{0,2}\.)?[a-z]+)(?: ((?:[0-9]{0,2}\.)?[a-z]+$))?}}
{
	#class {rogue} open;
	#variable {rpoi} {%3};
	#variable {rwep} {%2};
	#class {rogue} close;
	#if {"$rpoi" == "" && "$lastpoison" != ""}
	{
		#class {rogue} open;
		#variable {dolast} {0};
		#class {rogue} close;
		#switch {"$rwep"}
		{
			#case {"1"}
			{
				#variable {dolast} {1}
			};
			#case {"2"}
			{
				#variable {dolast} {1}
			}
		};
		#if {$dolast}
		{
			app $rwep $lastpoison[$rwep]
		};
		#else
		{
			#showme You say No poison was specified previously (specify a poison)!!
		};
		#unvariable {dolast}
	};
	#else
	{
		#if {"$rpoi" == ""}
		{
			#showme You say No poison was specified!!
		};
		#else
		{
			#switch {"$rwep"}
			{
				#case {"1"}
				{
					#variable {lastpoison[1]} {$rpoi};
					app primaryweapon $rpoi
				};
				#case {"2"}
				{
					#variable {lastpoison[2]} {$rpoi};
					app secondaryweapon $rpoi
				};
				#default
				{
					apply $rpoi $rwep
				}
			}
		}
	};
	#unvariable {rpoi};
	#unvariable {rwep}
}
{5}

#ALIAS {%i{autogar ?(on|of)?f?}}
{
	#format {agp} {%l} {%2};
	#switch {"$agp"}
	{
		#case {"on"}
		{
			#variable autogar 1
		};
		#case {"of"}
		{
			#variable autogar 0
		}
	};
	#unvariable {agp};
	#if {$autogar}
	{
		#showme {you say auto garrote is on}
	};
	#else
	{
		#showme {you say auto garrote is off}
	}
}
{5}

#ALIAS {hs}
{
	hamstring
}
{5}

#ALIAS {{(get|rem)picks}}
{
	#switch {"%2"}
	{
		#case {"get"}
		{
			rem dagger;
			get pouch sack;
			get picks pouch;
			hold picks
		};
		#case {"rem"}
		{
			rem picks;
			put picks pouch;
			put pouch sack;
			wield dagger
		}
	}
}
{5}

#ALIAS {{autotrip (on|off)}}
{
	#if {"%2" == "on"}
	{
		#variable {roguetrip} {1}
	};
	#else
	{
		#variable {roguetrip} {0}
	};
	#if {$roguetrip}
	{
		#variable triprep on
	};
	#else
	{
		#variable triprep off
	};
	#showme {you say autotrip is $triprep};
	#unvariable triprep
}
{5}

#MACRO {\eOP}
{
	say Ches
}

#VARIABLE         {autogar}  {0}
#VARIABLE         {lastpoison}  {}
#VARIABLE         {rogue}  {1}
#VARIABLE         {rogueassist}  {0}
#VARIABLE         {roguetrip}  {0}

#CLASS {rogue} CLOSE
