#CLASS {rogue} OPEN

#ACTION {Aaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaa rogue.tt$}
{
	#nop /* this action can be removed but is used to document the file and   */;
	#nop /* it's functionality.                                               */;
	#nop /* external dependencies...                                          */;
	#nop /*      none                                                         */;
	#nop /*                                                                   */;
	#nop /* aliases and usage...                                              */;
	#nop /*      {app <weapon> <poison>}                                      */;
	#nop /*           applies the specified poison to the specified weapon    */;
	#nop /*           if no poison is specified and this was used before      */;
	#nop /*           the last poison applied will be used.                   */;
	#nop /*                                                                   */;
	#nop /*      {autogar <on|off>}                                           */;
	#nop /*           will re-garrote a target if the target flips you off    */;
	#nop /*           if the on|off is omitted then it will report the status */;
	#nop /*           of auto garroting.                                      */;
	#nop /*                                                                   */;
	#nop /*      {bs <target>}                                                */;
	#nop /*           backstab the target                                     */
}
{1}

#ACTION {%i{(?:agrow sends .* sprawling with a powerful bash.)|(is sent sprawling)}}
{
	#echo {***cir***};
	cir
}
{5}

#ACTION {{.* does a nifty move, flipping you onto your back!}}
{
	st;
	#if {$autogar}
	{
		gar $enemy
	}
}
{5}

#ACTION {{And just for the attempt, you fall down.}}
{
	st
}
{5}

#ACTION {{As your poison takes effect, (.*?) appears to go blind!}}
{
	#showme {you say %2 is poison blind!};
	gsay %2 is poison blind!
}
{5}

#ACTION {{You try to trip .+, only to sprawl completely THROUGH .+.}}
{
	st
}
{5}

#ALIAS {%i{app ([a-zA-Z.0-9]+)(?: ([a-zA-Z.0-9]+))?}}
{
	#variable {rpoi} {%3};
	#if {"$rpoi" == "" && "$lastpoison" != ""}
	{
		#variable {rpoi} {$lastpoison}
	};
	#else
	{
		#if {"$rpoi" == ""}
		{
			#showme You say No poison was specified!!
		}
	};
	#variable {rwep} {%2};
	#echo {$rwep :: $rpoi};
	#switch {"$rwep"}
	{
		#case {"1"}
		{
			apply $rpoi primaryweapon
		};
		#case {"2"}
		{
			apply $rpoi secondaryweapon
		};
		#case {""}
		{
			apply $lastpoison $rwep
		};
		#default
		{
			apply $rpoi $rwep
		}
	};
	#unvariable {rpoi};
	#unvariable {rwep}
}
{5}

#ALIAS {%i{autogar(?: (on|off?))?}}
{
	#format {agp} {%l} {%2};
	#switch {"$agp"}
	{
		#case {"on"}
		{
			#variable autogar 1;
			autogar
		};
		#case {"of"}
		{
			#variable autogar 0;
			autogar
		};
		#default
		{
			#if {$autogar}
			{
				#showme {you say auto garrote is on}
			};
			#else
			{
				#showme {you say auto garrote is off}
			}
		}
	};
	#unvariable {agp}
}
{5}

#ALIAS {bs}
{
	backstab
}
{5}

#ALIAS {hs}
{
	hamstring
}
{5}

#MACRO {\eOP}
{
	say Ches
}

#VARIABLE         {autogar}  {0}
#VARIABLE         {lastpoison}  {}
#VARIABLE         {rogue}  {1}

#CLASS {rogue} CLOSE
